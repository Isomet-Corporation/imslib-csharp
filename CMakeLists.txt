cmake_minimum_required(VERSION 3.26)

message(STATUS "Building for Platform: '${CMAKE_GENERATOR_PLATFORM}'")
cmake_policy(SET CMP0091 NEW) # Required to avoid Conan toolchain error on Windows

project(imslib-csharp)

# Use C++17 or later
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

# --- Conan integration ------------------------------------------------------
if (MSVC)
   set(CONAN_TOOLCHAIN_FILE "${CMAKE_BINARY_DIR}/build/generators/conan_toolchain.cmake")
else()
   set(CONAN_TOOLCHAIN_FILE "${CMAKE_BINARY_DIR}/build/Release/generators/conan_toolchain.cmake")
endif()

message(STATUS "Conan toolchain at ${CONAN_TOOLCHAIN_FILE}")
include(${CONAN_TOOLCHAIN_FILE})

# --- SWIG ----------------------------------------------------------
find_package(SWIG 4.0 COMPONENTS csharp REQUIRED)
if(SWIG_FOUND)
  message(STATUS "SWIG found: ${SWIG_EXECUTABLE}")
  if(NOT SWIG_csharp_FOUND)
    message(WARNING "SWIG C# bindings cannot be generated")
  endif()
endif()

include(${SWIG_USE_FILE})

set(SWIG_MODULE_NAME iMSNETlib)

# The underlying C++ Library
set(IMS_LIB_SUBMODULE ext/ims-lib)

## iMS Dependencies generated by conan
#include(${CMAKE_BINARY_DIR}/generators/conan_toolchain.cmake) # Conan Toolchain (CMakeToolchain)
##
## Check for LibXML2
##
find_package(LibXml2 2.9.9 REQUIRED)
if (LibXml2_FOUND)
    message("-- Including LibXml2")
    include_directories(${libxml2_INCLUDE_DIRS})
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${libxml2_DEFINITIONS}")
else()
    message("-- LibXml2 not Found")
endif()

##
## Get BOOST
##
find_package(Boost REQUIRED)

# SWIG options:
#  -c++        : C++ mode
#  -directors  : Support SWIG Callbacks
#  -Wall       : Warnings
set(CMAKE_SWIG_FLAGS "-c++" "-directors" "-Wall")
#set(CMAKE_SWIG_FLAGS "-c++" "-Wall")

# Define the SWIG module
set(SWIG_INTERFACE swig/iMSNET.i)

set(CMAKE_SWIG_OUTDIR ${CMAKE_CURRENT_SOURCE_DIR}/dotnet)

set_property(SOURCE ${SWIG_INTERFACE} PROPERTY CPLUSPLUS ON)

set (UseSWIG_TARGET_NAME_PREFERENCE STANDARD)
swig_add_library(${SWIG_MODULE_NAME}
    TYPE MODULE
    LANGUAGE csharp
    SOURCES ${SWIG_INTERFACE}
) 

# Set source and include directories
target_include_directories(${SWIG_MODULE_NAME} PRIVATE
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${CSharp_INCLUDE_DIRS}
    ${CMAKE_CURRENT_SOURCE_DIR}/${IMS_LIB_SUBMODULE}/include
)

# Add iMS Library directly to this module
set(IMS_TARGET_NAME ${SWIG_MODULE_NAME})
add_subdirectory(${IMS_LIB_SUBMODULE})

if(MSVC)
    add_compile_options(/wd8029)  # Disable warning MSB8029 about building in Temporary Folder
endif()
